const int irLED = 7; //blue (with 220ohm) (all 4 on same pin)
//const int emitter = 4; //(vcc) (don't need)
const int collectors[] = {A0,A1,A2,A3}; //yellow (with pullup resistor) 
const int vibrations[] = {3,5,6,9}; //red
//orange goes to ground
//green goes to ground
//brown goes to ground

const int detectionThreshold = 1010;
const int whiteThreshold = 700;

int lastReadings[] = {1023, 1023, 1023, 1023};
int counters[] = {0, 0, 0, 0};
int counter2 = 0;

const int numFingers = 4;

void setup() {
  for(int i=0; i<numFingers; i++) {
    digitalWrite(collectors[i], HIGH);  // set pullup 
    pinMode(vibrations[i], OUTPUT);
   }
 
   pinMode(irLED, OUTPUT);
   digitalWrite(irLED, HIGH);
 
   Serial.begin(115200);       // use the serial port, this baud rate required for bluetooth (default bluetooth anyways)
}

void loop() {
  counter2++;
  for(int i=0; i<numFingers; i++) {
    counters[i]++;
    if(counters[i] > 4) {
      digitalWrite(vibrations[i], LOW);
    }
  }
  int readings[numFingers];
  for(int i=0; i<numFingers; i++) {
   readings[i] = analogRead(collectors[i]);
  }
  
  if(counter2 >= 5) {
    for(int i=0; i<numFingers-1; i++) {
      Serial.print(readings[i]);
      Serial.print(",");
    }
    Serial.println(readings[numFingers-1]);
    counter2 = 0;
  }
  
  for(int i=0; i<numFingers; i++) {
    if(counters[i] > 10 && lastReadings[i] < detectionThreshold && readings[i] < detectionThreshold && 
    ((lastReadings[i] < whiteThreshold && readings[i] >= whiteThreshold) || (lastReadings[i] >= whiteThreshold && readings[i] < whiteThreshold))) {
      digitalWrite(vibrations[i], HIGH);
      counters[i] = 0;
    }
    lastReadings[i] = readings[i];
   }
  
  delay(10);  // delay to avoid overloading the serial port buffer
}